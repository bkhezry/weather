apply plugin: 'com.android.application'
apply plugin: 'io.objectbox'

File googleServicesFile = rootProject.file("app/google-services.json")
if (googleServicesFile.exists()) {
    System.out.println("Google Service File Exist")
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}


def versionMajor = 2
def versionMinor = 3
def versionPatch = 0
android {
    compileSdk 34
    defaultConfig {
        applicationId "com.github.bkhezry.weather"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding = true
    }
    namespace 'com.github.bkhezry.weather'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    //use in development
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'

    implementation 'com.mikepenz:fastadapter:3.3.1'

    implementation 'com.github.bumptech.glide:glide:4.12.0'

    implementation 'com.airbnb.android:lottie:4.2.0'
    implementation 'com.github.bkhezry:MaterialSearchView:1.6.0'
    implementation 'com.github.bkhezry:prefser:v2.2.1-rx2'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    implementation 'io.github.inflationx:viewpump:2.0.3'
    implementation 'io.github.inflationx:calligraphy3:3.1.1'

    // Add the Firebase Crashlytics SDK.
    implementation 'com.google.firebase:firebase-crashlytics:18.6.0'
    // Recommended: Add the Google Analytics SDK.
    implementation 'com.google.firebase:firebase-analytics:21.5.0'

}
android.applicationVariants.all { variant ->
    def appName = "Weather"
    variant.outputs.all { output ->
        def newApkName
        newApkName = "${appName}-${output.baseName}-${variant.versionName}.apk"
        output.outputFileName = new File(newApkName)
    }
}
